Import('env')

package = {

    'requires': [],

    'sysroot_debs': [
        'libfontconfig1-dev', 'libmtdev-dev', 'libudev-dev', 'libts-dev', 'flex', 'freetds-dev',
        'libasound2-dev', 'libaudio-dev', 'libdbus-1-dev', 'libfreetype6-dev', 'libglib2.0-dev',
        'libjpeg-dev', 'libmng-dev', 'libpam0g-dev', 'libpng-dev', 'libreadline-dev', 'libssl1.0-dev',
        'libtiff-dev', 'zlib1g-dev', 'libjpeg-dev', 'libraspberrypi-dev',
    ],

    'root_debs': [
        'libraspberrypi0', 'libpng12-0', 'libglib2.0-0', 'libmtdev1', 'libts-0.0-0', 'libfontconfig1',
        'libfreetype6', 'libssl1.0.2', 'libjpeg62-turbo',
    ],

    'target': File('qt.tar.gz'),

}

qt_host = Dir('qt-host')
prefix = '/opt/qt'
stage = Dir('stage/')

repos = [Dir(d) for d in ['qtbase/', 'qtxmlpatterns/', 'qtdeclarative/']]

env['QT_HOST'] = qt_host

# TODO: depend on repo_files
env.Command([package['target']], ['${SYSROOT}'], [

    'rm -rf --one-file-system ${STAGE} ${QT_HOST}',
    'for repo in ${REPOS}; do git -C $$repo clean -dfxq; done',

    'cd ${REPOS[0]} && ./configure -release -opengl es2 -device linux-rasp-pi2-g++ \
		-qpa eglfs -no-libinput -no-linuxfb -no-xcb -no-kms -no-gbm \
		-no-gtk -no-widgets -no-compile-examples -no-sql-tds \
		-device-option CROSS_COMPILE=${CROSS_COMPILE} -sysroot ${SYSROOT.abspath} \
		-opensource -confirm-license -make libs -strip -optimize-size \
		-prefix ${PREFIX} -extprefix ${STAGE.abspath}/${PREFIX} -hostprefix ${QT_HOST.abspath}',

    'make -j8 -C ${REPOS[0]}',
	'make -j8 -C ${REPOS[0]} install',

	'cd ${REPOS[1]} && ${QT_HOST.abspath}/bin/qmake',
	'make -j8 -C ${REPOS[1]}',
	'make -j8 -C ${REPOS[1]} install',

	'cd ${REPOS[2]} && ${QT_HOST.abspath}/bin/qmake',
	'make -j8 -C ${REPOS[2]}',
	'make -j8 -C ${REPOS[2]} install',

    'mkdir -p ${STAGE}/etc/ld.so.conf.d',
    'echo ${PREFIX}/lib > ${STAGE}/etc/ld.so.conf.d/opt-qt.conf',

    'tar -C ${STAGE} --exclude=.${PREFIX}/doc --exclude=.${PREFIX}/include \
        --exclude=.${PREFIX}/lib/cmake --exclude=.${PREFIX}/lib/pkgconfig \
        -czf ${TARGETS[0]} .'

], REPOS=repos, PREFIX=prefix, STAGE=stage)

Return('package')

# Note: Install action for this package is just unpack the tarball.