Import('env')

kenv = env.Clone()

kenv['ENV']['ARCH'] = 'arm'
kenv['ENV']['CROSS_COMPILE'] = '../tools/arm-bcm2708/gcc-linaro-arm-linux-gnueabihf-raspbian/bin/arm-linux-gnueabihf-'


kenv['BUILDERS']['Kernel'] = Builder(action = [

    'git -C ${REPO} clean -dfxq',
    'rm -rf --one-file-system ${STAGE}',

    'cp ${SOURCES[0]} ${REPO}/.config',
    'make -j8 -C ${REPO} zImage modules dtbs',

    'mkdir -p ${STAGE}/root ${STAGE}/boot/overlays',
    'make -j8 -C ${REPO} INSTALL_MOD_PATH=${STAGE.abspath}/root modules_install',

    'cp ${REPO}/arch/arm/boot/zImage ${STAGE}/boot/${KERNEL_NAME}.img',
    'cp ${REPO}/arch/arm/boot/dts/*.dtb ${STAGE}/boot/',
    'cp ${REPO}/arch/arm/boot/dts/overlays/*.dtb* ${STAGE}/boot/overlays/',
    'cp ${REPO}/arch/arm/boot/dts/overlays/README ${STAGE}/boot/overlays/',

    'tar -C ${STAGE}/root/ -czf ${TARGETS[0]} .',
    'tar -C ${STAGE}/boot/ -czf ${TARGETS[1]} .',

])

repo = Dir('linux/')
repo_files = list(kenv['FUNCTIONS'].repo_scan(repo))

stage = Dir('stage/')

for kernel in ['kernel', 'kernel7']:

    root_gz = File(kernel + '-root.tar.gz')
    boot_gz = File(kernel + '-boot.tar.gz')
    config = File(kernel + '.config')

    k = kenv.Kernel([root_gz, boot_gz], [config, *repo_files], REPO=repo, KERNEL_NAME=kernel, STAGE=stage)
    kenv.SideEffect(stage, k) # this prevents parallel execution
    kenv.Clean(k, stage)
